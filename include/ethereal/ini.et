# TODO: add helper functions for working with ini, implement ini_write
import str;
import vec;
import map;
import fs;

fn ini_read( fname ) {
	f = fopen( fname, 'r' );
	if !f.is_open() { return INIRet.FNOP; }
	ini = {};
	curr_blk = '';
	ini.insert( '', {} );
	line = '';
	for ; f.read( line ); {
		line.trim();
		if line.empty() { continue; }
		# comments
		if line.front() == ';' { continue; }
		# make block
		if line.front() == '[' && line.back() == ']' {
			line.erase_at( 0 );
			line.erase_at( line.len() - 1 );
			curr_blk = line;
			ini.insert( curr_blk, {} );
			continue;
		}
		if !line.find( '=' ) { continue; }
		# make key value pairs
		kvp = line.split_first( '=' );
		kvp[ 0 ].trim();
		key = kvp[ 0 ];
		val = '';
		if kvp.len() > 1 {
			val = kvp[ 1 ].trim();
		}
		ini[ curr_blk ].insert( key, val );
	}
	if ini[ '' ].len() == 0 {
		ini.delete( '' );
	}
	return ini;
}